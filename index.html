<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>Sketchy, But Keeps Spinning</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="canvas"></canvas>
<textarea id="codeEditor" class="editor" spellcheck="false" autocorrect="off" autocapitalize="off" translate="no" oninput="render()"></textarea>
<pre id="error"></pre>
<div id="indicator"></div>
<div id="controls">
  <div class="controls">
    <input id="btnToggleView" class="icon" type="checkbox" name="toggleView" onclick="toggleView()">
    <input id="btnToggleResolution" class="icon" type="checkbox" name="toggleResolution" onchange="toggleResolution()">
    <input id="btnReset" class="icon" type="checkbox" name="reset" onclick="reset()">
  </div>
</div>
<script type="x-shader/x-fragment">#version 300 es
/*********
* made by Matthias Hurrle (@atzedent)
*/
precision highp float;
out vec4 O;
uniform float time;
uniform vec2 resolution;
#define FC gl_FragCoord.xy
#define R resolution
#define T time
#define N normalize
#define S smoothstep
#define SE(v,s,l) S(s+l/MN,s-l/MN,v)
#define MN min(R.x,R.y)
#define MAXD 10.
#define rot(a) mat2(cos((a)-vec4(0,11,33,0)))
#define sepia(a) vec3(dot(a,vec3(.4,.8,.28)),dot(a,vec3(.3,.7,.24)),dot(a,vec3(.2,.6,.18)))
float rnd(vec2 p) {
	p=fract(p*vec2(12.9898,78.233));
	p+=dot(p,p+34.56);
	return fract(p.x*p.y);
}
float noise(vec2 p) {
	vec2 i=floor(p), f=fract(p), u=f*f*(3.-2.*f), k=vec2(1,0);
	float
	a=rnd(i),
	b=rnd(i+k),
	c=rnd(i+k.yx),
	d=rnd(i+1.);
	return mix(mix(a,b,u.x),mix(c,d,u.x),u.y);
}
float map(vec3 p) {
	return length(p)-1.;
}
float march(inout vec3 p, vec3 rd) {
	float dd=.0;
	for (float i=.0; i<400.; i++) {
		float d=map(p);
		if (abs(d)<1e-3 || dd>MAXD) break;
		p+=rd*d;
		dd+=d;
	}
	return dd;
}
// by nimitz: https://www.shadertoy.com/view/4lfXDM
float texh(vec2 p, float s) {
	float d=1.;
	for (float i=.0; i<10.; i++) {
		float n=noise(p*vec2(1.25,25));
		n=S(-s*.1,2.3-s*.1,n);
		d=min(d,1.-n);
		p+=.07;
		p*=1.2;
		p=p.yx;
		if (s<i) break;
	}
	return 1.-d;
}
vec3 render(inout vec3 p, vec3 rd, inout vec3 lp) {
	vec3 col=vec3(0);
	float d=march(p,rd);
	if (d<MAXD) {
		vec3 n=N(p), l=N(lp-p);
		float dif=clamp(dot(l,n),.0,1.), spec=pow(clamp(dot(reflect(rd,n),l),.0,1.),8.),
		fres=pow(clamp(1.+dot(rd,n),.0,1.),5.);
		float msk=1.3-SE(sin(p.y*12.),.0,MN*.2);
		p*=6.;
		col+=msk*texh(p.xy,dif)*abs(n.z);
		col+=msk*texh(p.xz,dif)*abs(n.y);
		col+=msk*texh(p.yz,dif)*abs(n.x);
		col*=.2+dif+spec*.5;
		col+=.5*spec*(1.-msk);
		col=mix(col,vec3(0),fres);
	} else {
		// create gradient (diagonal) for the background
		vec2 uv=(FC-.5*R)/MN;
		uv+=vec2(1.8,1.15);
		uv*=1.5;
		float s=noise(12.+uv+noise(uv*5.+noise(uv*15.)));
		s+=distance(uv.x*.5,uv.y*.85);
		col=sin(vec3(((FC-.5*R)/MN)+.25,4.))+(1.-s);
		// add hatch texture along the gradient
		col*=texh((uv*.5)*rot(.67)*60.,s);
	}
	return col;
}
void cam(inout vec3 p) {
	p.yz*=rot(-.42);
	p.xz*=rot(-T*.2);
}
void main() {
	vec2 uv=(FC-.5*R)/MN;
	vec3 col=vec3(0),
	p=vec3(0,0,-4),
	lp=vec3(3,3,-3),
	rd=N(vec3(uv,1));
	cam(rd); cam(p); cam(lp);
	col=mix(col,render(p,rd,lp),min(time*.3,1.));
	col=sepia(col);
	col=max(col,mix(.0,.08,min(time*.3,1.)));
	O=vec4(col,1);
}</script>
<!-- partial -->
  <script  src="./script.js"></script>

</body>
</html>
